buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName '3.2.3'
    }
    lintOptions {
        abortOnError true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.6.0'
            force 'androidx.core:core-ktx:1.6.0'
        }
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    api 'com.facebook.react:react-native:+'
    api 'io.radar:sdk:3.2.4'
}

checkstyle {
    toolVersion = '9.1'
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

task checkstyle(type: Checkstyle) {
    dependsOn 'compileReleaseJavaWithJavac'
    source project.fileTree("$projectDir/src/main/java")
    classpath = project.files("$project.buildDir/intermediates/javac/release/classes")
    exclude '**/gen/**', '**/R.java', '**/BuildConfig.java'
}

check {
    dependsOn tasks.checkstyle
}