buildscript {
    ext {
        radar_sdk_version = '3.7.1-beta.8'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName '3.5.11'
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.6.0'
            force 'androidx.core:core-ktx:1.6.0'
        }
    }
}

def getSdkDependency(Project project) {
    //Check if this project contains the desired sdk in the libs directory
    File libs = project.file('libs')
    String sdkVersion = "$project.ext.radar_sdk_version"
    String aarFile = "sdk-$sdkVersion"
    boolean useLocalAar = libs.listFiles().find { it.name == "${aarFile}.aar".toString() }

    if (useLocalAar) {
        project.fileTree(dir: libs, include: ["${aarFile}.aar"])
    } else {
        "io.radar:sdk:$sdkVersion"
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    api 'com.facebook.react:react-native:+'
    api getSdkDependency(project)
}
